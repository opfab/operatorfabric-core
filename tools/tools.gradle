buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/libs-snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://repo.spring.io/libs-milestone" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        //classpath testing.junit5Plugin
        classpath boot.springBootPlugin
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: plugin.bom
//    apply plugin: plugin.boot

    dependencyManagement {
        imports {
            mavenBom cloud.dependencies
        }
    }

    dependencies {
        implementation misc.slf4j
        annotationProcessor misc.lombok
        implementation misc.lombok
        // compileOnly misc.lombok
        testRuntimeOnly testing.junit5Engine
        testImplementation testing.junit5Api
        testImplementation boot.starterTest, testing.assertJ, misc.lombok
    }

    configurations {
        testImplementation.exclude module: "junit"
    }

    /////// CONFIGURATION
    test {
        useJUnitPlatform()
        filter {
            includeTestsMatching "*Should"
        }
    }
    jacocoTestReport {
        group = "Reporting"
        reports {
            xml.enabled true
            csv.enabled false
            html.destination file("${buildDir}/reports/coverage")
        }
    }

    tasks.jacocoTestReport.dependsOn test

    /*Task that copies all the dependencies under build/libs so Sonar
    can make sense of the Lombok annotations for example */
    task copyDependencies(type: Copy) {
        description 'copy dependencies to build/support_libs'
        from configurations.compileClasspath
        into 'build/support_libs'
    }
}