plugins {
    id "base"
    id 'com.github.node-gradle.node'
    id "com.palantir.docker"
}

npmInstall {
    // Important: Use 'npm ci' instead of 'npm install' to ensure consistent dependencies.
    // 'npm ci' uses the exact versions from package-lock.json, preventing unexpected changes.
    npmCommand = ['ci'] 
    inputs.files(fileTree('node_modules'))
    inputs.file('package.json')
    inputs.file('package-lock.json')

    outputs.dir('build')

}

task runBuild(type: NpmTask) {
    inputs.files(fileTree('src'))
    inputs.files(fileTree('node_modules'))
    inputs.file('package.json')
    inputs.file('package-lock.json')

    outputs.dir('build')

    dependsOn npmInstall
    args = ['run', 'build']
}

task test(type: NpmTask) {
    inputs.files(fileTree('src'))
    inputs.files(fileTree('node_modules'))
    inputs.file('package.json')
    inputs.file('package-lock.json')
    inputs.file('jest.config.js')

    dependsOn runBuild
    args = ['run', 'test']
}

docker {
    name "lfeoperatorfabric/of-${project.name}:${project.version}"
    if (!project.version.equals("SNAPSHOT"))
        tag "latest", "latest"
    labels (['project':"${project.group}"])
    copySpec.with {
				from('build') {
					include 'domain/**'
                    include 'common/**'
                    include 'cardsReminder.js'
					into 'dockerBuild'
				}
                from('config') {
					include 'default-docker.yml'
					into 'dockerConfig'
				}
				from('.') {
					include 'startCardsReminder.sh'
					include 'package.json'
					include 'package-lock.json'
					include 'node_modules/**'
					into '.'
				}
			}
    buildArgs([
            'https_proxy'    : proxy.https
            ])
    dockerfile file("docker/Dockerfile")
}

tasks.runBuild.dependsOn(':node-services:node-common:npmInstall')

build.dependsOn(test)
build.dependsOn(runBuild)

assemble.dependsOn(runBuild)
tasks.dockerPrepare.dependsOn test

tasks.named("dockerPrepare") {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
