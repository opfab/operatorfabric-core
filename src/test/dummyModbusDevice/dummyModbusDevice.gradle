plugins {
	id "java"
	id "org.springframework.boot"
	id "com.palantir.docker"
	id "io.spring.dependency-management"
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

bootJar {
	manifest {
		attributes("Created-By"             : "Gradle ${gradle.gradleVersion}",
				"Specification-Title"    : "Dummy Modbus Device",
				"Implementation-Title"   : "Dummy Modbus Device",
				"Implementation-Version" : operatorfabric.version,
				"Specification-Version"  : operatorfabric.version
		)
	}
}


docker {
	name "lfeoperatorfabric/of-dummy-modbus-device:${project.version}"
	if (!project.version.equals("SNAPSHOT"))
		tag "latest", "latest"
    labels (['project':"${project.group}"])
	files( "build/libs")
	dockerfile file('src/main/docker/Dockerfile')
	buildArgs(['JAR_FILE'       : "dummyModbusDevice-${project.version.toUpperCase()}.jar",
               'https_proxy'    : proxy.https])
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'


    //Transitive dependencies udpate needed for security reasons 
    implementation misc.snakeyaml
    //End transitive dependencies
	
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	implementation misc.jlibmodbus
}

test {
	useJUnitPlatform()
}

// This will never actually serve as services rely on bootJars not jars
// It's just a temporary workaround so build/libs is not flagged as an implicit dependency of docker
jar {
	destinationDirectory.set(file("build/unused_plain_jar"))
}

tasks.docker.dependsOn build
tasks.dockerPrepare.dependsOn bootJar