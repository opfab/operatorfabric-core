plugins {
	id "java"
	id "org.springframework.boot"
	id "io.spring.dependency-management"
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

bootJar {
	manifest {
		attributes("Created-By"             : "Gradle ${gradle.gradleVersion}",
				"Specification-Title"    : "Dummy Modbus Device",
				"Implementation-Title"   : "Dummy Modbus Device",
				"Implementation-Version" : operatorfabric.version,
				"Specification-Version"  : operatorfabric.version
		)
	}
}



task buildDocker(type: Exec) {
	workingDir project.projectDir
	commandLine 'docker', 'build', 
				'--build-arg', "https_proxy=${proxy.https}",
				'--build-arg', "JAR_FILE=dummyModbusDevice-${project.version.toUpperCase()}.jar",
				'-t', "lfeoperatorfabric/of-dummy-modbus-device:${project.version}", '.'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	implementation misc.jlibmodbus
}

test {
	useJUnitPlatform()
}

// This will never actually serve as services rely on bootJars not jars
// It's just a temporary workaround so build/libs is not flagged as an implicit dependency of docker
jar {
	destinationDirectory.set(file("build/unused_plain_jar"))
}

tasks.buildDocker.dependsOn build