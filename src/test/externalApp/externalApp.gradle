
buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath misc.palantir
	}
}

apply plugin: plugin.bom
apply plugin: plugin.boot
apply plugin: 'java'
apply plugin: plugin.dockerBuild


configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

bootJar {
	manifest {
		attributes("Created-By"             : "Gradle ${gradle.gradleVersion}",
				"Specification-Title"    : "OperatorFabric Businessconfig Parties Manager",
				"Implementation-Title"   : "OperatorFabric Businessconfig Parties Manager",
				"Implementation-Version" : operatorfabric.version,
				"Specification-Version"  : operatorfabric.version
		)
	}
}


docker {
	if (project.version.toUpperCase().equals("SNAPSHOT"))
		name "lfeoperatorfabric/of-external-app:SNAPSHOT" /* more information : https://vsupalov.com/docker-latest-tag/ */
	else
		name "lfeoperatorfabric/of-external-app"
	tags "latest", "${project.version}"
    labels (['project':"${project.group}"])
	files( "build/libs")
	dockerfile file('src/main/docker/Dockerfile')
	buildArgs(['JAR_FILE'       : "externalApp-${project.version.toUpperCase()}.jar",
                       'http_proxy'     : apk.proxy.uri,
                       'https_proxy'    : apk.proxy.uri,
                       'HTTP_PROXY_AUTH': "basic:*:$apk.proxy.user:$apk.proxy.password"])
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
	}
}

test {
	useJUnitPlatform()
}

// This will never actually serve as services rely on bootJars not jars
// It's just a temporary workaround so build/libs is not flagged as an implicit dependency of docker
jar {
	destinationDirectory.set(file("build/unused_plain_jar"))
}

tasks.docker.dependsOn build
tasks.dockerPrepare.dependsOn bootJar