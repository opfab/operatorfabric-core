subprojects {

    apply plugin: "org.springframework.boot"
    apply plugin: "org.hidetake.swagger.generator"

    /////// COMPILATION

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
                srcDir 'build/swagger/src/main/java'
            }
            resources{
                srcDir 'src/main/resources'
                srcDir 'build/src/generated/resources'
            }
        }
    }

    /////// CONFIGURATION


    swaggerSources {
        endpoints {
            inputFile = file('src/main/modeling/swagger.yaml')
            code {
                language = 'org.opfab.generators.OpfabSpringGenerator'
                additionalProperties = [ignoreImportMappings:"false"]
                configFile = file('src/main/modeling/config.json')
                outputDir = file("./build/swagger")
                templateDir = file('../../src/main/resources/swagger-templates')
            }
            ui {
                outputDir = file("./build/docs/api")
            }
        }
    }

    // This is a temporary fix to avoid false build error, see (#1543)
    tasks.named("processResources") {
        duplicatesStrategy = 'include'
    }

    tasks.compileJava.dependsOn generateSwaggerCode
    tasks.compileTestJava.dependsOn generateSwaggerCode
    tasks.generateSwaggerCodeEndpoints.dependsOn ':tools:swagger-spring-generators:assemble'
}

